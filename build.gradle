plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.2'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'jacoco'
	id 'maven-publish'
}

group = 'com.homedepot.supplychain'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
	developmentOnly
	runtimeClasspath {
		extendsFrom developmentOnly
	}
	compileOnly {
		extendsFrom annotationProcessor
	}
}

tasks.named("bootBuildImage") {
	builder = "dashaun/builder:tiny"
	imageName = 'scope-native'
	bindings = [
			"${project.projectDir}/bindings:/platform/bindings:ro"
	]
}

repositories {
	maven {
		url "https://maven.artifactory.homedepot.com/artifactory/libs-release/"
		credentials {
			username = System.getenv("GRADLEW_USERNAME")
			password = System.getenv("GRADLEW_PASSWORD")

		}
	}
	maven {
		url "https://maven.artifactory.homedepot.com/artifactory/libs-snapshot"
		credentials {
			username = System.getenv("GRADLEW_USERNAME")
			password = System.getenv("GRADLEW_PASSWORD")
		}
	}
	maven {
		url 'https://maven.artifactory.homedepot.com/artifactory/plugins-release'
		credentials {
			username = System.getenv("GRADLEW_USERNAME")
			password = System.getenv("GRADLEW_PASSWORD")
		}
	}
	maven {
		url "https://maven.artifactory.homedepot.com/artifactory/libs-snapshot-local"
		credentials {
			username = System.getenv("GRADLEW_USERNAME")
			password = System.getenv("GRADLEW_PASSWORD")
		}
	}
	mavenCentral()
	mavenLocal()
	maven {
		url "https://repo.spring.io/libs-milestone"
	}
}

ext {
	set('springCloudGcpVersion', "4.5.1")
	set('springCloudVersion', "2020.0.3")
	set('springFrameworkVersion', "6.2.1")
}

dependencies {

	//Spring
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation "org.springframework.integration:spring-integration-core:${springFrameworkVersion}"
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'

	//Spring Cloud GCP Dependencies
	implementation 'com.google.cloud:spring-cloud-gcp-autoconfigure'
	implementation("com.google.cloud:spring-cloud-gcp-starter-pubsub")
	implementation("com.google.cloud:spring-cloud-gcp-starter-bigquery")
	implementation ('com.google.cloud:spring-cloud-gcp-starter')

	//Logback
	implementation ('com.google.cloud:spring-cloud-gcp-starter-logging')
	implementation ('net.logstash.logback:logstash-logback-encoder:7.0')

	//JSON,File,IO related
	implementation 'com.networknt:json-schema-validator:1.0.72'
	implementation 'commons-io:commons-io:2.16.1'

	//Upgrade vulnerable dependencies version
	implementation 'org.apache.commons:commons-compress:1.26.0'
	implementation "org.springframework:spring-core:${springFrameworkVersion}"
	implementation "org.springframework:spring-beans:${springFrameworkVersion}"
	implementation "org.springframework:spring-expression:${springFrameworkVersion}"
	implementation "org.springframework:spring-context:${springFrameworkVersion}"
	implementation "org.springframework:spring-webmvc:${springFrameworkVersion}"
	implementation "org.springframework:spring-web:${springFrameworkVersion}"
	implementation 'io.netty:netty-common:4.1.115.Final'
	implementation 'com.google.guava:guava:33.2.1-jre'
	implementation 'com.google.protobuf:protobuf-java:4.28.2'
	implementation 'org.json:json:20240205'

	//Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'

	//OSC-R2R-Commons
	implementation('com.homedepot.osc.run2run:osc-run2run-commons:1.4.1')

	//Test
	testImplementation 'org.xmlunit:xmlunit-core:2.10.0'
	testImplementation 'org.mockito:mockito-inline:2.13.0'
	testImplementation 'org.testcontainers:junit-jupiter:1.19.2'
	testImplementation 'javax.inject:javax.inject:1'
	testImplementation 'org.testcontainers:gcloud:1.19.1'
	testImplementation ('org.springframework.boot:spring-boot-starter-test') {
	}
}

dependencyManagement {
	imports {
		mavenBom "com.google.cloud:spring-cloud-gcp-dependencies:${springCloudGcpVersion}"
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/integration-test/java')
		}
		resources.srcDir file('src/integration-test/resources')
	}
}

configurations {
	integrationTestCompile.extendsFrom (testCompile)
	integrationTestRuntime.extendsFrom (testRuntime)
	integrationTestImplementation.extendsFrom (testImplementation)
	integrationTestRuntimeOnly.extendsFrom(testRuntimeOnly)
	integrationTestCompileOnly.extendsFrom(testCompileOnly)
	integrationTestAnnotationProcessor.extendsFrom(testAnnotationProcessor)
}

tasks.register('integrationTest', Test) {
	description = "Run integration tests"
	group = "verification"
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.named('integrationTest') {
	useJUnitPlatform()
}

test {
	finalizedBy jacocoTestReport // report is always generated after tests run
}
jacocoTestReport {
	dependsOn test // tests are required to run before generating the report

	reports {
		xml.required = true
	}

	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: 'com/homedepot/supplychain/enterpriselabormanagement/EnterpriseLaborManagementApplication.class')
		}))
	}
}

jacoco {
	toolVersion = "0.8.8"
}